BACKUP ~dsr/backup~
//AUTHOR ~TIN, Badgert (WeiDU conversion)~
SUPPORT ~Spellhold Studios at http://www.shsforums.net/forum/127-mod-resurrections/~

VERSION ~v8.0.0~

README ~dsr/readme/dsr-readme-%LANGUAGE%.txt~ ~dsr/readme/dsr-readme-english.txt~


ALWAYS

	/* Converts tra files to UTF-8 */
	ACTION_DEFINE_ARRAY tin_reload BEGIN setup.tra END
	ACTION_DEFINE_ARRAY tin_noconvert BEGIN END
	LAF HANDLE_CHARSETS
		INT_VAR
			infer_charsets = 1
		STR_VAR
			tra_path = EVAL ~%MOD_FOLDER%/lang~
			noconvert_array = tin_noconvert
			reload_array = tin_reload
	END

	/* Loads in any shared functions library (general macros and functions) */
	INCLUDE ~dsr/lib/gw_functions.tpa~	// Loads macros and functions library dealing with tooltip appending and specific EE items descriptions.

END

AUTO_TRA ~dsr/lang/%s~

LANGUAGE ~English~
		 ~english~
         ~dsr/lang/english/setup.tra~

LANGUAGE ~Russian (translation by aerie.ru)~
		 ~russian~
         ~dsr/lang/russian/setup.tra~

LANGUAGE ~Francais (traduction : Deratiseur)~
		 ~french~
         ~dsr/lang/french/setup.tra~

LANGUAGE ~Italiano (traduzione di ilot)~
		 ~italian~
         ~dsr/lang/italian/setup.tra~

LANGUAGE ~Deutsch (Uebersetzung von Jarl)~
		 ~german~
         ~dsr/lang/german/setup.tra~

LANGUAGE ~Chinese~
		 ~schinese~
         ~dsr/lang/schinese/setup.tra~

/* ================================================================ *
 *    Demon Summoning Ritual - (Unholy Gate Opening Ritual Book)    *
 * ================================================================ */

BEGIN @0
DESIGNATED 0 LABEL ~demon_summoning_ritual~
REQUIRE_PREDICATE GAME_INCLUDES ~tob~ @1

// Install items
COPY ~dsr/itm/sumdem.itm~	~override~
	SAY NAME1 @11
	SAY NAME2 @12
	SAY UNIDENTIFIED_DESC @13
	LPF ~GW_WRITE_EE_ITM_DESCRIPTIONS~ STR_VAR GW_desc_to_update = "@14" END

// Install creatures
COPY ~dsr/cre/balorsu.cre~	~override~
COPY ~dsr/cre/demcorsu.cre~	~override~
	SAY NAME1 @21
	SAY NAME2 @21

//COPY ~dsr/cre/tolger2.cre~       ~override/tolger2.cre~
COPY_EXISTING ~tolger2.cre~ ~override~
	PATCH_IF SOURCE_SIZE > 0x2d3 BEGIN
		WRITE_BYTE 0x72 0
		WRITE_BYTE 0x83 100
		WRITE_BYTE 0x237 2
		ADD_KNOWN_SPELL ~SPWI113~ #0 ~wizard~
		ADD_KNOWN_SPELL ~SPWI212~ #1 ~wizard~
		ADD_KNOWN_SPELL ~SPWI313~ #2 ~wizard~
		ADD_KNOWN_SPELL ~SPWI712~ #6 ~wizard~
		ADD_KNOWN_SPELL ~SPWI815~ #7 ~wizard~
		ADD_KNOWN_SPELL ~SPWI909~ #8 ~wizard~
		ADD_KNOWN_SPELL ~SPWI910~ #8 ~wizard~
		ADD_KNOWN_SPELL ~SPWI911~ #8 ~wizard~
		ADD_MEMORIZED_SPELL ~SPWI113~ #0 ~wizard~ (1)
		ADD_MEMORIZED_SPELL ~SPWI212~ #1 ~wizard~ (1)
		ADD_MEMORIZED_SPELL ~SPWI313~ #2 ~wizard~ (1)
		ADD_MEMORIZED_SPELL ~SPWI712~ #6 ~wizard~ (1)
		ADD_MEMORIZED_SPELL ~SPWI815~ #7 ~wizard~ (1)
		ADD_MEMORIZED_SPELL ~SPWI909~ #8 ~wizard~ (1)
		ADD_MEMORIZED_SPELL ~SPWI910~ #8 ~wizard~ (1)
		ADD_MEMORIZED_SPELL ~SPWI911~ #8 ~wizard~ (1)
		ADD_CRE_ITEM ~SUMDEM~ #1 #0 #0 ~NONE~ ~QUIVER2~
	END
BUT_ONLY

COPY ~dsr/eff~ ~override~

// Install scripts
//COPY_EXISTING ~tolger.bcs~	~override/tolger.bcs~
EXTEND_BOTTOM ~tolger.bcs~	~dsr/scripts/stolger.baf~

COMPILE ~dsr/baf~

// End of Demon Summoning Ritual
